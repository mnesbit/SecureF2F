import static org.gradle.api.JavaVersion.VERSION_21

plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'idea'
    id 'application'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'idea'
}

allprojects {
    group 'uk.co.nesbit'
    version securef2f_version

    repositories {
        mavenCentral()
    }

    tasks.withType(JavaCompile).configureEach {
        sourceCompatibility = VERSION_21
        targetCompatibility = VERSION_21
        options.encoding = 'UTF-8'
        options.compilerArgs += '-XDenableSunApiLintControl'
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            languageVersion = '2.0'
            apiVersion = '2.0'
            jvmTarget = VERSION_21
            javaParameters = true   // Useful for reflection.
            freeCompilerArgs = ['-Xjvm-default=all']
        }
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()

        // Prevent the project from creating temporary files outside of the build directory.
        systemProperty 'java.io.tmpdir', layout.buildDirectory.asFile.get().absolutePath
        systemProperty 'org.slf4j.simpleLogger.defaultLogLevel', 'error'
        maxHeapSize = '4096m'
    }


    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
        implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
        implementation "org.slf4j:slf4j-api:$slf4j_version"

        // Test utilities
        testImplementation "org.junit.jupiter:junit-jupiter-api:$junit5_version"
        testImplementation "org.junit.jupiter:junit-jupiter-params:$junit5_version"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit5_version"
        testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
        testImplementation "org.jetbrains.kotlin:kotlin-test-junit5:$kotlin_version"
        testRuntimeOnly "org.slf4j:slf4j-simple:$slf4j_version"
    }
}

wrapper {
    gradleVersion = "8.8"
    distributionType = Wrapper.DistributionType.ALL
}

dependencies {
    runtimeOnly "org.apache.logging.log4j:log4j-slf4j2-impl:$log4j2_version"
    // for lock-free logging
    runtimeOnly "com.lmax:disruptor:$disruptor_version"
    // Bouncy castle
    implementation "org.bouncycastle:bcprov-jdk18on:$bouncycastle_version"
    implementation "org.bouncycastle:bcpkix-jdk18on:$bouncycastle_version"
    // Command line processing
    implementation "info.picocli:picocli:$picocli_version"


    implementation project(':avro')
    implementation project(':crypto')
    implementation project(':network')
}

application {
    mainClass = "uk.co.nesbit.MainKt"
}